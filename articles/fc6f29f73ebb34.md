---
title: 'fish shellでROS1/2の設定'
emoji: 🐠
type: tech
topics: [fish, ros, ros1, ros2]
published: true
---

みなさんは**fish**というshellをご存知でしょうか？

https://fishshell.com

fishとは**f**riendly **i**nteractive **sh**ellの略で、その名の通りfriendlyつまり使いやすさを謳ったshellです．個人的にはあまり設定を頑張らなくてもそれなりに便利に使えるといった感じで、そういうの面倒だなと感じている人におすすめです．

一応欠点も書いておくと、POSIX互換ではないのでshやbashのスクリプトは普通には動かせないことや、bashやzshに比べると情報が少ないというのがあります．前者については[bass](https://github.com/edc/bass) というパッケージがありますし、インストールコマンドぐらいならそのときだけbashに切り替えればよいので、個人的にはあまり困っていません．後者についても、少ないといってもドキュメントがありますし、調べるとなんだかんだStack Overflowあたりが引っかかるので、英語がある程度読めればあまり困らないと思います．

fishの入門記事を書くつもりはないので、詳しく知りたい方は「fish shell」とかで調べて手頃な記事を読んでみてください．何か記事を紹介できるとよいのですが、自分が最初に読んだ記事も大分記述が古くなってきている^[fishは現在3系ですが2と3で大きく変わりましたし、パッケージマネージャのfisherのインストールコマンドは2回変わりました．install(v2) => add(v3) => install(v4)（なぜ戻した）．パッケージ名もちょくちょく変わっていたり．]のであまり勧められるものがなかったりします……．

ということで早速本題に．あまりfishを使っている人自体少ないのではと思いますが、「ROS1/2使うからfishは我慢してbash/zsh使っています」みたいな人に向けて、**fishでもROSできるよ**ということをお伝えしつつ、あわよくばfish使う人増えたらいいなみたいな記事です．

## 準備

ユーティリティ関数として、以下の `command_exist` だけ用意しておきます．ほぼ見たとおりだと思いますが、第1引数のコマンドが存在するかどうかを調べる関数です．

```fish
function command_exist
    if type $argv[1] >/dev/null 2>&1
        return 0
    else
        echo $argv[1]: not found
        return 1
    end
end
```

以下のように使います．

```terminal
$ if command_exist echo; echo OK; end
OK
$ if command_exist xxx; echo OK; end
xxx: not found
```

## ROS1

bashのスクリプトを読みこむために、以下のパッケージがあると便利なのでREADMEに沿ってインストールします．

https://github.com/edc/bass

自分は何となく最初にfisherを入れて、特に困っていないのでそのまま使っていますが、oh-my-fishやfundleでもいいと思います．

https://github.com/jorgebucaran/fisher

https://github.com/oh-my-fish/oh-my-fish

https://github.com/danhper/fundle

ROS1の設定は以下の通りです．

```fish:ros1.fish
set -l dists
set -a dists kinetic # Ubuntu 16.04
set -a dists melodic # Ubuntu 18.04
set -a dists noetic # Ubuntu 20.04

for dist in $dists
    set -l path /opt/ros/$dist/setup.bash
    if test -f $path
        bass source $path
    end
end

if command_exist roscore
    if set -q ros1_ip
        set -x ROS_HOSTNAME $ros1_ip
    else
        set -x ROS_HOSTNAME localhost
    end

    set -x ROS_MASTER_URI http://$ROS_HOSTNAME:11311

    set -l path /opt/ros/$ROS_DISTRO/share/rosbash/rosfish
    if test -f $path
        source $path
    end

    set -l path ~/catkin_ws/devel/setup.bash
    if test -f $path
        bass source $path
    end
end
```

一応部分毎に解説を．

```fish
set -l dists
set -a dists kinetic # Ubuntu 16.04
set -a dists melodic # Ubuntu 18.04
set -a dists noetic # Ubuntu 20.04

for dist in $dists
    set -l path /opt/ros/$dist/setup.bash
    if test -f $path
        bass source $path
        break
    end
end
```

`set -l`はローカル変数です．`-a`は配列に対するappendで、ちなみに`-p`がprependです．何となくの可読性でこう書いていますが、下のように一気に構築したり、変数を介さなくてもよいです．

```fish
set -l dists kinetic melodic noetic
```

```fish
for dist in kinetic melodic noetic
```

`for`文に関しては`do`や`done`がなくてわかりやすくないでしょうか？

（ところで、改めて見ると少しRubyっぽいなと思いました．個人的にRubyの文法は結構好きなので、その共通点に惹かれたところもあるのかもしれません．Rubyはfor自体あまり書かないとかはさておき．）

---

```fish
    if set -q ros1_ip
        set -x ROS_HOSTNAME $ros1_ip
    else
        set -x ROS_HOSTNAME localhost
    end

    set -x ROS_MASTER_URI http://$ROS_HOSTNAME:11311
```

`set -q`で変数の存在チェックをしています．fishには**ユニバーサル変数**という変数があり、これは全てのセッションで共有され、ログアウト等しても保持され続けます．この性質からシェル等の設定変数として使うことができます．
まさに`ros1_ip`がそのユニバーサル変数としてセットされることが想定されており、`set -U ros1_ip xxx.xxx.xxx.xxx`とすると、このスクリプト自体の評価タイミングがあるので即座とはいかないものの、新たにタブを開いたり、既に開いているものでも`exec fish`等でリロードすれば設定が反映されるので非常に便利です．

- ユニバーサル変数についての参考

https://fishshell.com/docs/current/#more-on-universal-variables

---

```fish
    set -l path /opt/ros/$ROS_DISTRO/share/rosbash/rosfish
    if test -f $path
        source $path
    end
```

`roscd`等のコマンドを使えるようにするためと、補完を効かせるために読み込みます．これはfishスクリプトなので、bassは不要です．一時期スクリプトが壊れていてGitHubから最新のファイル取ってこないと動かないこともありましたが、~~最近は安定しています~~．
と思っていたら補完が上手く動かなくて、git log見てたらバグ見つけたのでPR送りました．

https://github.com/ros/ros/pull/279

---

```fish
    set -l path ~/catkin_ws/devel/setup.bash
    if test -f $path
        bass source $path
    end
```

こちらはfishスクリプトが吐かれないので、bassを使って読み込みます．

## ROS2

```fish:ros2.fish
set -l dists
set -a dists dashing # Ubuntu 18.04
set -a dists eloquent # Ubuntu 18.04
set -a dists foxy # Ubuntu 20.04

for dist in $dists
    set -l path /opt/ros/$dist/setup.bash
    if test -f $path
        bass source $path
    end
end

if command_exist ros2
    if command_exist register-python-argcomplete
        register-python-argcomplete --shell fish ros2 | source
    end

    set -l path ~/ros2/install/setup.bash
    if test -f $path
        bass source $path
    end
end
```

ほぼROS1と同じなのでほとんど省略しますが一箇所だけ．

```fish
    if command_exist register-python-argcomplete
        register-python-argcomplete --shell fish ros2 | source
    end
```

`register-python-argcomplete`はargcompleteというPyPiパッケージの一部で、コマンド引数を扱うのに便利な標準ライブラリであるargparseからコマンド補完を作ってくれるやつです．

https://pypi.org/project/argcomplete/

argcompleteはfishをサポートしてくれているので、パイプを通して`source`で読み込めばよいです．

## ROS1 + ROS2

ROS1、ROS2のどちらかだけを使うならこれらで十分ですが、自分はdotfileをGitHubに上げて複数の環境で共有しているので、様々な環境で使いやすいようにしました．
まず、ROS1、ROS2それぞれの設定ファイルを、`ros1.fish`, `ros2.fish`として切り出して、以下の`ros.fish`から条件に応じて呼び分けています．

```fish:ros.fish
set -e ROS_DISTRO

function ros1_load
    source $FISH_SCRIPTS_DIR/ros1.fish
end

function ros2_load
    source $FISH_SCRIPTS_DIR/ros2.fish
end

if set -q use_only_ros1 && test $use_only_ros1 -ne 0
    ros1_load
    echo "ROS1 (ROS_MASTER_URI: $ROS_MASTER_URI)"
else if set -q use_only_ros2 && test $use_only_ros2 -ne 0
    ros2_load
    echo ROS2
end
```

`use_only_ros1(2)`はまたもやユニバーサル変数です．この環境ではどちらか片方しか使わないと決まっていれば、変数をセットしておいて起動時に設定を読み込みます．
どちらも使う場合、勝手に読まれても困るので、`ros1(2)_load`を明示的に読み込むようにしました．

## おわりに

`rosfish`に**バグがなければ**、fishでもちゃんと補完が効くので、fish本来の使いやすさと相まって結構快適に動かせると思います．特にros2の方はargcompleteを使っているので、`rosfish`よりも安定しているでしょう．
